plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'com.vaadin' version '0.14.3.7'
}

defaultTasks("clean", "vaadinBuildFrontend", "build")

repositories {
	mavenCentral()
	maven {
        url "http://maven.vaadin.com/vaadin-addons"
    }
}

ext {
	set('vaadinVersion', "14.4.1")
}

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
}

dependencies {
	implementation 'org.smartbit4all.platform:ui-vaadin'
	implementation project(':bee-ui-vaadin')

	api 'org.smartbit4all.platform:sql'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'	
	implementation('com.vaadin:vaadin-spring-boot-starter') {
//         Webjars are only needed when running in Vaadin 13 compatibility mode
		["com.vaadin.webjar", "org.webjars.bowergithub.insites",
		 "org.webjars.bowergithub.polymer", "org.webjars.bowergithub.polymerelements",
		 "org.webjars.bowergithub.vaadin", "org.webjars.bowergithub.webcomponents"]
				.forEach { group -> exclude(group: group) }
	}
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

dependencyManagement {
	imports {
		mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
	}
}

test {
	useJUnitPlatform()
}

vaadin {
	pnpmEnable = true
}

vaadinBuildFrontend {
	doLast{
		file('build/vaadin-generated/.keep').text=""
	}
}



// default profile
def profiles = "dev"
if (project.hasProperty("mock")) {
    profiles = "mock"
    apply from: "gradle/profile_mock.gradle"
} else if (project.hasProperty("uat")) {
    profiles = "uat"
    apply from: "gradle/profile_uat.gradle"
} else if (project.hasProperty("prod")) {
    profiles = "prod"
    apply from: "gradle/profile_prod.gradle"
} else if (project.hasProperty("dev")) {
	apply from: "gradle/profile_dev.gradle"
	println "Applied dev as parameter."
} else {
	apply from: "gradle/profile_dev.gradle"
	println "Applied dev as default."
}

println "Profile used for Gradle build: [$profiles]."
println "You can change it with adding [prod|uat|dev|mock] argument to Gradle build. For example: 'gradle bootRun -Pmock'"
println "For development in Eclipse IDE use the project specific gradle properties to set this argument!"



processResources {
    //inputs.property('springProfiles', profiles)
    filesMatching("**/application.yml") {
        filter {
            it.replace("#spring.profiles.active#", profiles)
        }
    }
}

